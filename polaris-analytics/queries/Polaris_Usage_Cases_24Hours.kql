let GatewayRequests = AppRequests
| where AppRoleName == 'fa-polaris-gateway'
    and Properties.User != 'REDACTED@REDACTED.EMAIL.ADDRESS'    
| where TimeGenerated > datetime_add('hour', -24, now());

let CaseCounts = GatewayRequests
| where Name == 'PolarisPipelineCase'
| summarize CaseCount = count_distinct(extract("cases/(.*)$", 1,Url)) by bin(TimeGenerated, 1h);

let CaseDocumentViewedCounts = GatewayRequests
| where Name == 'PolarisPipelineGetDocument'
| summarize CaseDocumentViewedCount = count_distinct(extract("cases/(.*)/documents", 1,Url)) by bin(TimeGenerated, 1h);

let CaseDocumentRedactedCounts = AppEvents
| where Name == "RedactedDocument"
| where Properties.isFailure != "true"
| project TimeGenerated, CaseId = tolong(Properties.caseId)
| summarize CaseDocumentRedactedCount = count_distinct(CaseId) by bin(TimeGenerated, 1h);

let BrokenCaseCounts = Polaris_Exceptions_Core
| summarize BrokenCaseCount = count_distinct(CaseId) by TimeGenerated = bin(EarliestExceptionTimeGenerated, 1h);

CaseCounts
| join kind=fullouter CaseDocumentViewedCounts on TimeGenerated
| join kind=fullouter CaseDocumentRedactedCounts on TimeGenerated
| join kind=fullouter BrokenCaseCounts on TimeGenerated
| project 
    TimeGenerated, 
    CaseCount, 
    BrokenCaseCount
    // CaseDocumentViewedCount,
    // CaseDocumentRedactedCount = coalesce(CaseDocumentRedactedCount,0)
| render columnchart kind=unstacked 