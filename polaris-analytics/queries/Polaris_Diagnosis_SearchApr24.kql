AppRequests
| where AppRoleName == "fa-polaris-coordinator"
| where Name == "ExtractText"
| project TimeGenerated, OperationId, ActivitySuccess = Success
| join kind=leftouter (AppExceptions
        | where AppRoleName == "fa-polaris-coordinator"
        | where OperationName == "ExtractText"
        | summarize arg_max(TimeGenerated, InnermostMessage) by OperationId
        | project OperationId, ExceptionType = case(
            InnermostMessage contains "502", "502",
            InnermostMessage contains "503", "503",
            InnermostMessage)
) on OperationId | project-away OperationId1

| join kind=leftouter (AppRequests
    | where AppRoleName == "fa-polaris-text-extractor"
    | where Name == "ExtractText"
    | project OperationId, 
        TextExtractorSuccess = ResultCode == 200, 
        ActivityDurationMs = DurationMs,
        ResultCode
) on OperationId | project-away OperationId1

| join kind = leftouter(
    AppTraces
    | where AppRoleName == "fa-polaris-text-extractor"
    | where OperationName == "ExtractText"
    | where Properties.prop__MethodName == "GetOcrResultsAsync"
    | where Message contains "OCR completed"
    | project 
        OperationId,
        //TimeGenerated,  AppRoleInstance, PolarisCorrelationId = tostring(Properties.prop__CorrelationId), 
        OcrDurationMs = (extract("in (\\d+)", 1, Message))
) on OperationId | project-away OperationId1

| join kind = leftouter (

    AppDependencies
    | where Target == "polarisprod.cognitiveservices.azure.com"
    | summarize TotalOcrHttpCalls = count(), TotalFailedOcrHttpCalls =countif(ResultCode !in("200", "202")) by OperationId
) on OperationId | project-away OperationId1

| join kind = leftouter (

    Polaris_Metrics_Conversion
    | project PolarisCorrelationId, DocumentId, Megabytes
    | join (Polaris_Metrics_Indexing | project PolarisCorrelationId, DocumentId, OperationId)
    on PolarisCorrelationId, DocumentId
    | project OperationId, Megabytes
    
) on OperationId | project-away OperationId1


| extend Status = case ( 
    isnull(TextExtractorSuccess), "1 - TextExtractorNotReached",
    ActivitySuccess and TextExtractorSuccess, "5 - Success",
    ActivitySuccess and not(TextExtractorSuccess), "4 - TextExtractorFail",
    not(ActivitySuccess) and not(TextExtractorSuccess), "3 - CompoundFail",
    not(ActivitySuccess) and TextExtractorSuccess, "2- Long OCR",
    "Other"
)


//| summarize count() by Status, bin(TimeGenerated, 1h)


