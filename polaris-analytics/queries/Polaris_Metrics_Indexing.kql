AppEvents
| where Name == "IndexedDocument"
| project 
    TimeGenerated, 
    OperationId,
    AppRoleInstance,
    CaseId = toint(Properties.caseId),
    PolarisCorrelationId = tostring(Properties.correlationId),
    DocumentId = tolong(Properties.documentId),
    DocumentTypeId = toint(Properties.documentTypeId),
    DocumentCategory = tostring(Properties.documentCategory),
    StartTime = todatetime(Properties.startTime),
    EndTime = todatetime(Properties.endTime),
    DurationSeconds = toreal(Measurements.durationSeconds),
    IndexDurationSeconds = toreal(Measurements.indexDurationSeconds),
    IndexSettledDurationSeconds = toreal(Measurements.indexSettledDurationSeconds),
    LineCount = toint(Measurements.lineCount),
    OcrDurationSeconds = toreal(Measurements.ocrDurationSeconds),
    PageCount = toint(Measurements.pageCount),
    WordCount = toint(Measurements.wordCount),
    IsFailure = tobool(Properties.isFailure) == true,
    DidIndexSettle = tobool(Properties.didIndexSettle),
    WaitRecordCounts = parse_json(strcat("[",Properties.waitRecordCounts, "]"))
| extend 
    DidRecordCountWobble = tostring(array_sort_asc(WaitRecordCounts)) != tostring(WaitRecordCounts),
    WaitCheckCount = array_length(WaitRecordCounts)
    
