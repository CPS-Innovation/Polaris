let isKnownBug = (
    method: dynamic, 
    exceptionType: dynamic, 
    outerMessage: dynamic, 
    innermostMessage:dynamic, 
    operationName: dynamic, 
    cmsStatusCode: dynamic,
    urn: dynamic,
    loadBalancedDestination: dynamic,
    appRoleName: dynamic) {

    let me = tostring(method);
    let et = tostring(exceptionType);
    let om = tostring(outerMessage);
    let im = tostring(innermostMessage);
    let op = tostring(operationName);
    let csc = tostring(cmsStatusCode);
    let u = tostring(urn);
    let lbd = tostring(loadBalancedDestination);
    let arn = tostring(appRoleName);

    case(
                om contains "An exception occurred contacting Cms Modern" and om contains "Status Code: Unauthorized", 
        "27573 - Cms Modern 401 error",
                me contains "polaris_ddei.Factories.PreCharge.PcdRequestSummaryFactory.WalkTable",
        "25170 - DDEI walktable mapping error on PCD request",
                csc contains "502",
        "BadGateway - proxy cannot reach CMS",
                op contains "Ping",
        "Ping - Ping failure",
    
                om contains "System.OutOfMemoryException" and op contains "ConvertToPdf",
        "24379 - System.OutOfMemoryException in Pdf Generator",

                me contains "Ddei.Mappers.CaseDetailsMapper.FindHeadlineCharge",
        "24083 - Headline charge mapping error",

                op contains "RedactPdf"
        and om contains "At most 4 elements (for any collection) can be viewed in evaluation mode",
        "22794 - Aspose 4 elements",

                im contains "aspose" and im contains "expired" and (im contains "licence" or im contains "license"),
        "ASPOSE - Aspose licence has expired",

                csc contains "302" and u contains "InitiateCookies",
        "23611 - Auth fail InitiateCookies",

                csc contains "302" and u notcontains "InitiateCookies",
        "23613 - Auth fail CMS post-InitiateCookies",

        om contains "header or cookie is expected" or im contains "Cms Auth Token cannot be null",
        "Auth fail - missing Cms-Auth-Values",

                csc contains "413",
        "22626 - Upload document too large",

                om contains "Expected UID cookie value not found",
        "23617 - UserId not found InitiateCookies",

               csc contains "504" or im contains "GatewayTimeout",
        "23687 - Gateway Timeouts to Cms Modern",

               csc contains "503",
        "23614 - CMS/Proxy Service Unavailble",

                lbd contains '""' and csc != "",
        "23619 - Load balancing wrong",
        
                op contains "/polaris-ui"
        and om contains "Uncaught TypeError: Cannot read properties of undefined (reading 'rotation')",
        "22624 - UI Rotation",

            (op contains "ExtractText" or op contains "RefreshDocumentOrchestratorNext" or op contains "RefreshDocumentOrchestrator")
        and (om contains "The request was canceled due to the configured HttpClient.Timeout of 100 seconds elapsing."
                or 
             om contains "Search index update failed, timeout waiting for indexation validation")
                or om contains "Exception while executing function: ExtractText"
                or om contains "Exception while executing function: RefreshDocumentOrchestratorNext"
                or om contains "Exception while executing function: RefreshDocumentOrchestrator", 
        "23495 - Search Index",

            op contains "ExtractText"
        and om contains "Operation returned an invalid status code 'BadRequest'", 
        "23496 - ComputerVision Bad Request",

            op contains "PolarisPipelineCase"
        and om contains "An Orchestration instance with the status Running already exists",
        "22956 - Orchestration Running",

            (me contains "MapCaseDetails"
        and im contains "Value cannot be null.") or om contains "Mapping error(s) found", 
         "23498 - Mapping Case/Document Details",

            op contains "GeneratePdf"
        and me contains "ConvertToPdfAsync"
        and (et contains "HttpRequestException" or et contains "SocketException"),
        "23502 - Convert Pdf 500 Errors (e.g. image export)",

            op contains "GeneratePdf"
        and im contains "'HTM'",
        "23615 - HTM document conversion",

            op contains "GeneratePdf"
        and im contains "'MHT'",
        "24091 - HTM document conversion",

            op contains "RedactPdf"
        and om contains "ConversionLog.xml' is denied",
        "23505 - Redact Conversion.log access denied",

            op contains "GetDocument"
        and im contains "The specified blob does not exist",
        "27576 - GetDocument - Blob does not exist",

        om contains "Buffer too small for CLOB to CHAR or BLOB to RAW conversion",
        "24757 - CLOB to CHAR or BLOB to RAW conversion",

            (csc == '' or csc == 200 or csc == 401) and et contains "GraphQL.Client.Http.GraphQLHttpRequestException",
        "23616 - Auth fail CmsModern post-InitiateCookies",

            me contains "Netherite" and om contains "Timeout",
        "23754 - Netherite timeouts",

            op contains "SlidingCaseClearDown" and (om contains "Value must be between 0 and 100000"),
        "25042 - SlidingCaseCleardown broken on large case index",

            op contains "SlidingCaseClearDown" and (om contains "Timeout" or om contains "502"),
        "23755 - SlidingCaseClearDown timeouts",

            op contains "GeneratePdf" and im contains "Requested value" and im contains "was not found.",
        "24381 - Pdf Conversion on unknown file type",

            op contains "ListDocuments" and om contains "Error converting value {null} to type 'System.Int32'. Path 'data.attachments",
        "24539 - DDEI statement.witness mapping error",
            im contains "Connection reset by peer" and op contains "GeneratePdf",
        "24709 - Pdf generator rejects coordinator http connection",
            et contains "Microsoft.IdentityModel.Tokens.SecurityTokenExpiredException",
        "24710 - AD token expiry",
            et contains "CmsModernAuthorizationException" and om  contains "User is not authorized to access this case",
        "25015 - User not authorised to view a case",
            op contains "PolarisPipelineSaveDocumentRedactions",
        "24767 - Save Redactions(unclassified elsewhere)",
            op contains "polaris-ui",
        "25171 - UI exceptions",
            et contains "System.IndexOutOfRangeException",
        "25834 - IndexOutOfRengeException",
            om contains "The namespace declaration attribute has an incorrect 'namespaceURI': 'http://www.w3.org/2000/xmlns/",
        "26163 - Aspose -The namespace declaration attribute has an incorrect 'namespaceURI': 'http://www.w3.org/2000/xmlns/",
            op contains "Status" or op contains "Health", 
        "Status func timeout",
            et contains "OcrServiceException" and im contains "The stream was already consumed.",
        "26424 - OCR retry fail",
            om matches regex "Document \\d+ not found in case \\d+",
        "26677 - Redacted doc id no longer present",
        im contains "Error converting value {null} to type 'System.Int32'. Path 'data.inboxCommunications",
        "276035 - DDEI error on InboxCommunication with null MaterialId",
        om contains "Object reference not set to an instance of an object" and me contains "GetTrackerDocumentResponse.GetBlobName",
        "27038 - Post incident: error on not being able to retrieve document from tracker",
        et contains "Aspose.Slides.InvalidPasswordException",
        "27443 - password protected Powerpoint",
        me contains "PolarisGateway.Clients.Coordinator.CoordinatorClient" and om contains "HttpClient.Timeout of 100 seconds elapsing",
        "Gateway timeout calling coordinator", 
        op contains "GetCase" and om contains "Error while copying content to a stream.",
        "27446 - DDEI GetCase: Error while copying content to a stream",
        (op contains "InitiateIndex" or op contains "CompleteIndex") and (om contains "HttpClient.Timeout" or om contains "502" or im contains "Connection reset by peer"),
        "27574 - Timeouts and 502s contacting Text Extractor",
        op contains "InitiateOcr" or op contains "CompleteOcr",
        "27575 - Computer Vision errors",
        om contains "no longer exists in CMS: This case has been deleted",
        "CMS Case Deleted",
        op contains "CompleteIndex" or op contains "InitiateIndex" or op contains "StoreCaseIndexes" and im contains "502" or im contains "503" or im contains "Value cannot be null" or 
        im contains "Unexpected character encountered while parsing value" or om contains "At least one indexing action failed",
        "27614 - index 502/503",
        op contains "GeneratePdf" and om contains "The request was canceled due to the configured HttpClient.Timeout of 100 seconds elapsing",
        "Pdf Generator timeouts",
        op contains "InitiateCookies" or op contains "GetCasePcdRequests" and (om contains "NotFound" or csc == 404 or im contains "The resource cannot be found") and (om contains "uainGeneratedScript" or im contains "uainGeneratedScript" or om contains "uapcPreChargeDecRequests"),
        "27980 - CMS Classic Not Found uainGeneratedScript",
        arn contains "fa-polaris-pdf-generator" and om contains "Object reference not set to an instance of an object",
        "Aspose - Object reference not set to an instance of an object",
    "");
};

Polaris_Exceptions_Core
| where not (tostring(CmsStatusCode) contains "302" and tostring(LoadBalancedDestination) contains '""')
| where not (
    tostring(OuterMessage) contains "Cms Modern" 
    and tostring(OuterMessage) notcontains "ServiceUnavailable" 
    and strcmp(format_datetime(EarliestExceptionTimeGenerated, 'yyyy-MM-dd-HH:mm'), '2023-08-30-10:09') < 0
)
| extend 
    IsKnownBug = isKnownBug(Method, ExceptionType, OuterMessage ,InnermostMessage ,OperationName, CmsStatusCode,  Urn, LoadBalancedDestination, AppRoleName ),
    PolarisCorrelationId = tostring(PolarisCorrelationId)    
| where AppRoleName !contains "staging"
// filter out known fixes
| where not (IsKnownBug startswith("22624")) 
    // known incidents of 23614
| where not (PolarisCorrelationId in ('aa8cdd9f-c04f-4ee0-ac24-ead4babdace3', 'b914e3cb-8410-488e-be53-1aad352451e6'))
    // historical Aspose licence errors
| where not (IsKnownBug startswith "ASPOSE" and strcmp(format_datetime(EarliestExceptionTimeGenerated, 'yyyy-MM-dd-HH:mm'), '2023-09-07-10:00') < 0)
| where InnermostMessage notcontains "HTE" and OuterMessage notcontains "HTE" 
| where IsKnownBug !startswith "25042"
| where not(IsKnownBug startswith "23502" and OuterMessage contains "Pdf is encrypted")
// case deleted in cms
| where not(IsKnownBug startswith "CMS Case Deleted")
// user unauthed is accounted for elsewhere
| where ExceptionType !contains "CmsModernAuthorizationException"
// noise from slot-swap
| where not(AppRoleName contains "staging" and OuterMessage contains "Status")
| where not(IsKnownBug startswith "24767" and (PolarisCorrelationId in ("00000000-0000-0000-0000-000000000000", "e2e00000-0000-0000-0000-000000000000")))

| where not(IsKnownBug == "Auth fail - missing Cms-Auth-Values" and User == "")
// filter out e2e tests that are intentionally omitting CmsAuthValues
| where not( IsKnownBug == "" and ExceptionType contains "CmsAuthenticationException" and User == "")

// remove Status func timeouts from dashboard for the time being, reduce the noise
| where IsKnownBug != "Status func timeout"
